plugins {                   //<1>
    id 'groovy'             //<2>
    id 'application'        //<3>
    id 'build-dashboard'    //<4>
    id 'project-report'     //<5>
    id 'codenarc'           //<6>
    id 'jacoco'             //<7>

    //This is a handy plugin as it rolls everything into a single Jar file: https://github.com/johnrengelman/shadow/
    id 'com.github.johnrengelman.shadow' version '1.2.2'    //<8>

    //The idea and eclipse plugins provide helpers for 2 popular IDEs
    id 'eclipse'            //<9>
    id 'idea'               //<10>
}

repositories {              //<11>
    //The JCenter repository holds libraries that we can use in our code: https://bintray.com/bintray/jcenter
    jcenter()
}

project.ext {                           //<12>
    groovyVersion = '2.4.4'             //<13>
    spockVersion = '1.0-groovy-2.4'     //<14>
    gradleVersion = '2.7'               //<15>
    codeNarcVersion = '0.24'            //<16>
}

//project.name is set in the settings.gradle file using: rootProject.name = 'shapes-demo'
project.with {  //<17>
    group 'org.groovytutorial.shapes'
    version '1.0-SNAPSHOT'
    description = 'A tutorial covering basic Gradle usage and OO-development in Groovy'

    defaultTasks 'run'  //<18>

    //This is the class that is called when using the "run" task
    mainClassName = 'org.groovytutorial.shapes.app.Main'    //<19>

    wrapper.gradleVersion = "$gradleVersion"                //<20>
}

dependencies {  //<21>
    //This brings in the Groovy system:
    compile group: 'org.codehaus.groovy', name: 'groovy-all', version: "$groovyVersion"

    //This brings in the Spock Testing Framework for the test stage
    testCompile group: 'org.spockframework', name: 'spock-core', version: "$spockVersion"
}

//CodeNarc analyses the source code for possible improvements
codenarc {  //<22>
    toolVersion = "$codeNarcVersion"

    codenarcMain {
        configFile file('config/codenarc/ruleset.groovy')
        exclude "**/package-info.*"
        ignoreFailures false
        maxPriority1Violations 0
        maxPriority2Violations 10
        maxPriority3Violations 20
    }

    codenarcTest {
        ignoreFailures true
        configFile file('config/codenarc/ruleset-test.groovy')
        maxPriority1Violations 0
        maxPriority2Violations 10
        maxPriority3Violations 20
    }
}
